plugins {
    id 'java-library'
}

configurations {
    nativeSampleImplementation.extendsFrom implementation
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    api "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

    // Add SLF4J runtime adapter for JDK logging for GraalVM native-image build of MathService/Tool
    nativeSampleImplementation "org.slf4j:slf4j-jdk14:${slf4jVersion}"

    testImplementation "org.codehaus.groovy:groovy:${groovyVersion}:indy"
    testImplementation ("org.codehaus.groovy:groovy-json:${groovyVersion}:indy") {
        transitive = false
    }

    testRuntimeOnly "org.slf4j:slf4j-jdk14:${slf4jVersion}"
}

ext.moduleName = 'org.consensusj.jsonrpc'
def mainClassName = "org.consensusj.jsonrpc.introspection.sample.MathService"

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes  'Implementation-Title': 'ConsensusJ JSON-RPC Sample tool w/introspection',
                'Automatic-Module-Name': moduleName,
                'Main-Class': mainClassName,
                'Implementation-Version': archiveVersion.get()
    }
}

test {
    testLogging.showStandardStreams = true
    systemProperty 'java.util.logging.config.file', "${project.projectDir}/src/test/logging.properties"
    beforeTest { descriptor ->
        logger.lifecycle('    ' + descriptor.getName())
    }
}

// Compile a native image of the MathService tool sample using Graal's native-image tool
// Graal must be installed at $GRAALVM_HOME
task nativeImage(type:Exec, dependsOn: jar) {
    workingDir = projectDir
    executable = "${System.env.GRAALVM_HOME}/bin/native-image"
    args = [ '--verbose',
             '--no-fallback',
             '-cp', "${-> configurations.nativeSampleImplementation.asPath}", // Lazy configuration resolution
             '-jar', jar.archiveFile.get(),
             '-H:Path=build',
             '-H:Name=MathTool',
             '--initialize-at-build-time=org.consensusj.jsonrpc.introspection.sample.MathService,org.slf4j.helpers.NamedLoggerBase,org.slf4j.impl.JDK14LoggerAdapter,org.slf4j.helpers.MarkerIgnoringBase,org.slf4j.helpers.Util',
             '-H:+AllowIncompleteClasspath',
             '-H:+ReportUnsupportedElementsAtRuntime',
             '-H:+ReportExceptionStackTraces'
    ]
}
