import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
    id 'java-library'
}
apply plugin: 'net.ltgt.errorprone'

dependencies {
    api "org.jspecify:jspecify:1.0.0"
    api "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    api "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

    testImplementation project(':cj-btc-jsonrpc')

    testImplementation "org.apache.groovy:groovy:${groovyVersion}"
    testImplementation ("org.apache.groovy:groovy-json:${groovyVersion}") {
        transitive = false
    }

    testRuntimeOnly "org.slf4j:slf4j-jdk14:${slf4jVersion}"

    annotationProcessor "com.uber.nullaway:nullaway:${nullAwayVersion}"
    testAnnotationProcessor "com.uber.nullaway:nullaway:${nullAwayVersion}"
    errorprone "com.google.errorprone:error_prone_core:${errorProneVersion}"
}

tasks.withType(JavaCompile) {
    options.errorprone {
        disable("FunctionalInterfaceMethodChanged")  // TODO: Fix this in JsonRpcClientJavaNet
        check("NullAway", CheckSeverity.ERROR)
        option("NullAway:OnlyNullMarked")
    }
}

jar {
    manifest {
        attributes  'Implementation-Title': 'ConsensusJ JSON-RPC Client',
                'Implementation-Version': archiveVersion.get()
    }
}

test {
    testLogging.showStandardStreams = true
    systemProperty 'java.util.logging.config.file', "${project.projectDir}/src/test/logging.properties"
    beforeTest { descriptor ->
        logger.lifecycle('    ' + descriptor.getName())
    }
}
