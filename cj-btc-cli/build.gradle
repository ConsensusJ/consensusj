/*
 * CLI tool support library and `cj-bitcoin-cli` tool.
 * `cj-bitcoin-cli` tool is compiled to a native command-line tool with Graal `native-image`
 */
plugins {
    id 'java-library'
}

ext.moduleName = 'org.consensusj.bitcoin.cli'

tasks.withType(JavaCompile).configureEach {
    options.release = 21
}

configurations {
    nativeToolImplementation.extendsFrom implementation
}

dependencies {
    api project(':consensusj-jsonrpc-cli')
    api project(':cj-btc-jsonrpc')

    // Add SLF4J runtime adapter for JDK logging for GraalVM native-image build of bitcoin CLI tool
    nativeToolImplementation "org.slf4j:slf4j-jdk14:${slf4jVersion}"

    testImplementation "org.apache.groovy:groovy:${groovyVersion}"

    testRuntimeOnly "org.slf4j:slf4j-jdk14:${slf4jVersion}"
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }
        regTest(JvmTestSuite) {
            dependencies {
                implementation project()
                implementation "org.apache.groovy:groovy:${groovyVersion}"
                implementation("org.spockframework:spock-core:${spockVersion}") {
                    exclude module: "groovy-all"
                }

                runtimeOnly "net.bytebuddy:byte-buddy:1.17.7"
                // allows Spock to mock classes (in addition to interfaces)
                runtimeOnly "org.objenesis:objenesis:3.4"
                // Allow Spock to mock classes with constructor arguments
                runtimeOnly "org.slf4j:slf4j-jdk14:${slf4jVersion}"   // Runtime implementation of slf4j
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                        outputs.upToDateWhen { false }
                    }
                }
            }
        }
    }
}

def mainClassName = "org.consensusj.bitcoin.cli.BitcoinCLITool"

jar {
    manifest {
        attributes 'Implementation-Title': 'ConsensusJ Bitcoin CLI tool',
                    'Automatic-Module-Name': moduleName,
                    'Main-Class': mainClassName,
                    'Implementation-Version': archiveVersion.get()
    }
}

// Compile a native image using GraalVM's native-image tool
// Graal must be installed at GRAALVM_HOME
tasks.register('nativeCompile', Exec) {
    dependsOn jar
    workingDir = projectDir
    executable = "${System.env.GRAALVM_HOME}/bin/native-image"
    args = ['--verbose',
            '--no-fallback',
            '-cp', "${-> configurations.nativeToolImplementation.asPath}", // Lazy configuration resolution
            '-jar', jar.archiveFile.get(),
            '-H:Path=build',
            '-H:Name=cj-bitcoin-cli',
            '-H:EnableURLProtocols=http,https',
            '-H:+ReportUnsupportedElementsAtRuntime',
            '-H:-CheckToolchain'
    ]
}
