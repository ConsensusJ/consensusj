plugins {
    id 'java-library'
}

ext.moduleName = 'org.consensusj.jsonrpc.groovy'

tasks.withType(GroovyCompile) {
    targetCompatibility = 9
}

dependencies {
    api project(':consensusj-jsonrpc')

    implementation "org.apache.groovy:groovy:${groovyVersion}"

    testImplementation ("org.apache.groovy:groovy-json:${groovyVersion}") {
        transitive = false
    }
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes  'Implementation-Title': 'ConsensusJ JSON-RPC Groovy Support',
                'Automatic-Module-Name': moduleName,
                'Implementation-Version': archiveVersion.get()
    }
}

test {
    testLogging.showStandardStreams = true
    systemProperty 'java.util.logging.config.file', "${project.projectDir}/src/test/logging.properties"
    beforeTest { descriptor ->
        logger.lifecycle('    ' + descriptor.getName())
    }
}
