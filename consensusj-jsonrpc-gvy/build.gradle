plugins {
    id 'java-library'
}

sourceCompatibility = 9
targetCompatibility = 9

dependencies {
    api project(':consensusj-jsonrpc')
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"       // Loggable trait is part of API
    implementation "org.apache.groovy:groovy:${groovyVersion}"

    testImplementation ("org.apache.groovy:groovy-json:${groovyVersion}") {
        transitive = false
    }
}

ext.moduleName = 'org.consensusj.jsonrpc.groovy'

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes "Automatic-Module-Name": moduleName
    }
}

test {
    testLogging.showStandardStreams = true
    systemProperty 'java.util.logging.config.file', "${project.projectDir}/src/test/logging.properties"
    beforeTest { descriptor ->
        logger.lifecycle('    ' + descriptor.getName())
    }
}
