import net.ltgt.gradle.errorprone.CheckSeverity

/*
 * `jrpc` JSON-RPC tool. Can be compiled to a native command-line tool with Graal `native-image`
 */
plugins {
    id 'java'
    id 'application'
}
apply plugin: 'net.ltgt.errorprone'

tasks.withType(JavaCompile).configureEach {
    options.release = 21
}

configurations {
    nativeToolImplementation.extendsFrom implementation
}

dependencies {
    implementation project(':consensusj-jsonrpc-cli')
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-toml:${jacksonVersion}"

    runtimeOnly "org.slf4j:slf4j-jdk14:${slf4jVersion}"
    nativeToolImplementation "org.slf4j:slf4j-jdk14:${slf4jVersion}"

    annotationProcessor "com.uber.nullaway:nullaway:${nullAwayVersion}"
    errorprone "com.google.errorprone:error_prone_core:${errorProneVersion}"
}

application {
    mainModule = "org.consensusj.jrpc"
    mainClass = 'org.consensusj.jrpc.JRpc'
    applicationName = 'jrpc'
}

tasks.withType(JavaCompile) {
    options.errorprone {
        check("NullAway", CheckSeverity.ERROR)
        option("NullAway:OnlyNullMarked")
    }
}

testing {
    suites {
        configureEach {
            useJUnitJupiter()
            dependencies {
                implementation platform("org.junit:junit-bom:${junitJupiterVersion}")

                implementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
                runtimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"

                runtimeOnly "org.slf4j:slf4j-jdk14:${slf4jVersion}"

                annotationProcessor "com.uber.nullaway:nullaway:${nullAwayVersion}"
            }
        }
        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

jar {
    manifest {
        attributes  'Implementation-Title': 'ConsensusJ JSON-RPC CLI Tool',
                    'Main-Class': application.mainClass,
                    'Implementation-Version': archiveVersion.get()
    }
}

// Compile a native image using GraalVM's native-image tool
// GraalVM must be installed at GRAALVM_HOME
tasks.register('nativeCompile', Exec) {
    dependsOn jar
    workingDir = projectDir
    executable = "${System.env.GRAALVM_HOME}/bin/native-image"
    args = [
        '--verbose',
        '--no-fallback',
        '-cp', "${-> configurations.nativeToolImplementation.asPath}", // Lazy configuration resolution
        '-jar', jar.archiveFile.get(),
        '-H:Path=build',
        "-H:Name=$application.applicationName",
        '--initialize-at-build-time=com.fasterxml.jackson.annotation.JsonProperty$Access',
        '-H:IncludeResources=logging.properties',
        '-H:EnableURLProtocols=http,https',
        '-H:+ReportUnsupportedElementsAtRuntime',
        '-H:+ReportExceptionStackTraces',
        '-H:-CheckToolchain'  // Needed for building inside Nix sandbox
    ]
}
