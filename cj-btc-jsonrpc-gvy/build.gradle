plugins {
    id 'java-library'
    id 'groovy'
}

ext.moduleName = 'org.consensusj.bitcoin.jsonrpc.groovy'

tasks.withType(GroovyCompile) {
    targetCompatibility = 9
}

dependencies {
    api project(':cj-btc-jsonrpc')
    api project(':consensusj-jsonrpc-gvy')

    implementation "org.apache.groovy:groovy:${groovyVersion}"
    implementation ("org.apache.groovy:groovy-json:${groovyVersion}") {
        transitive = false
    }

    testImplementation project(':cj-bitcoinj-dsl-gvy')                // Use Groovy extensions in tests
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes  'Implementation-Title': 'ConsensusJ Bitcoin JSON-RPC Groovy Support',
                'Automatic-Module-Name': moduleName,
                'Implementation-Version': archiveVersion.get()
    }
}

test {
    testLogging.showStandardStreams = true
    systemProperty 'java.util.logging.config.file', "${project.projectDir}/src/test/logging.properties"
    beforeTest { descriptor ->
        logger.lifecycle('    ' + descriptor.getName())
    }
}
