testing {
    suites {
        configureEach {
            useJUnitJupiter()
            dependencies {
                implementation project()
                implementation project(':cj-btc-jsonrpc-gvy')
                implementation project(':cj-btc-json')
                implementation project(':cj-btc-services')      // To test WalletAppKitService
                implementation project(':cj-bitcoinj-util')     // For BlockUtils
                implementation project(':cj-bitcoinj-dsl-gvy')  // Use Groovy extensions in tests

                implementation "org.slf4j:slf4j-api:${slf4jVersion}"

                implementation("org.spockframework:spock-core:${spockVersion}") {
                    exclude module: "groovy-all"
                }
                implementation "org.apache.groovy:groovy:${groovyVersion}"
                implementation ("org.apache.groovy:groovy-json:${groovyVersion}") {
                    transitive = false
                }

                runtimeOnly "net.bytebuddy:byte-buddy:1.17.7"       // Allows Spock to mock classes (in addition to interfaces)
                runtimeOnly "org.objenesis:objenesis:3.4"           // Allows Spock to mock classes with constructor arguments
                runtimeOnly "org.slf4j:slf4j-jdk14:${slf4jVersion}" // Runtime implementation of slf4j
            }
        }
        test {
            targets {
                all {
                    testTask.configure {
                        testLogging.showStandardStreams = true
                        systemProperty 'java.util.logging.config.file', "${project.projectDir}/src/test/logging.properties"
                        beforeTest { descriptor ->
                            logger.lifecycle('    ' + descriptor.getName())
                        }
                    }
                }
            }
        }
        regTest(JvmTestSuite) {
            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                        outputs.upToDateWhen { false }
                        testLogging.showStandardStreams = true
                        beforeSuite { descriptor ->
                            if (descriptor.getClassName() != null) {
                                logger.lifecycle('\033[1m' + descriptor.getName() + "\033[0m") // bold
                            }
                        }
                        beforeTest { descriptor ->
                            logger.lifecycle('    ' + descriptor.getName())
                        }
                        systemProperty 'regtest', true
                        systemProperty 'java.util.logging.config.file', "${project.projectDir}/src/regTest/logging.properties"
                        if (project.hasProperty("regTestUseLegacyWallet")) {
                            systemProperty "regTestUseLegacyWallet", project.property("regTestUseLegacyWallet")
                        }
                        include 'org/consensusj/bitcoin/rpc/**',
                                'org/consensusj/bitcoin/integ/bitcoinj/**',
                                'org/consensusj/bitcoin/integ/funding/**',
                                'org/consensusj/bitcoin/integ/services/**',
                                'org/consensusj/bitcoin/integ/java/**'
                    }
                }
            }
        }
    }
}
