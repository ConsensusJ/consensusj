buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.asciidoctor.jvm.convert'    version '4.0.4'
    id 'org.ajoberstar.git-publish'     version '4.2.2'
}

gradle.rootProject {
    apply plugin: 'distribution'
}

develocity {
    buildScan {
        publishing.onlyIf { System.getenv("CI") != null }
        termsOfUseUrl = 'https://gradle.com/terms-of-service'
        termsOfUseAgree = 'yes'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'test-report-aggregation'
    //apply plugin: 'findbugs'

    version = consensusjVersion     // set in gradle.properties
    group = 'com.msgilligan'

    repositories {
        mavenCentral()
    }

//    tasks.withType(FindBugs) {
//        reports {
//            xml.enabled false
//            html.enabled true
//        }
//    }
}

subprojects {
    dependencies {
        implementation "org.slf4j:slf4j-api:${slf4jVersion}"

        testImplementation("org.junit.jupiter:junit-jupiter:${junitJupiterVersion}")
        testImplementation "org.apache.groovy:groovy:${groovyVersion}"
        testImplementation("org.spockframework:spock-core:${spockVersion}") {
            exclude module: "groovy-all"
        }

        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
        testRuntimeOnly "net.bytebuddy:byte-buddy:1.16.1"                 // allows Spock to mock classes (in addition to interfaces)
        testRuntimeOnly "org.objenesis:objenesis:3.4"                     // Allow Spock to mock classes with constructor arguments
        testRuntimeOnly  "org.slf4j:slf4j-jdk14:${slf4jVersion}"          // Runtime implementation of slf4j
    }

    configurations.configureEach {
        resolutionStrategy {
            force "org.apache.groovy:groovy:${groovyVersion}"
            force "org.apache.groovy:groovy-json:${groovyVersion}"
        }
        // Ensure usage of Groovy 4.0 which has new Maven co-ordinates
        resolutionStrategy.capabilitiesResolution.all {
            if (capability.group.equals("org.codehaus.groovy")) {
                selectHighestVersion()
            }
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    compileJava {
        options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
        options.encoding = 'UTF-8'
        options.release = 11
    }

    tasks.withType(GroovyCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(AbstractArchiveTask).configureEach {
        // This should result in reproducible JAR builds
        // See: https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    tasks.withType(Jar).configureEach {
        // Normalize permissions for reproducible builds
        dirPermissions {
            unix("0755")
        }
        filePermissions {
            unix("0644")
        }
    }

    tasks.withType(Javadoc).configureEach {
        // Support reproducible JavaDoc builds
        options.noTimestamp = true
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()              // We're using Spock 2 and JUnit 5
    }
}

apply from: 'gradle/idea.gradle'
apply from: 'gradle/javadoc.gradle'
apply from: 'gradle/asciidoctor.gradle'
apply from: 'gradle/github-pages.gradle'
apply from: 'gradle/maven-publish.gradle'

dependencies {
    testReportAggregation project(':').subprojects
}

reporting {
    reports {
        testAggregateTestReport(AggregateTestReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('testAggregateTestReport', TestReport)
}

build.dependsOn subprojects.build

tasks.register('buildCI') { dependsOn build, javadocAll /*, asciidoctor */ }

tasks.named('installDist').configure {
    destinationDir = layout.buildDirectory.dir('artifacts').get().getAsFile()
}

distributions {
    main {
        contents {
            project.subprojects.each { sub ->
                into(sub.name) {
                    from sub.jar
                    from sub.sourcesJar
                    from sub.javadocJar
                    from sub.generatePomFileForJarPublication
                    from sub.generateMetadataFileForJarPublication
                }
            }
        }
    }
}
