buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.asciidoctor:asciidoctorj:${asciidoctorjVersion}"
        classpath "org.asciidoctor:asciidoctorj-diagram:${asciidoctorjDiagramVersion}"
    }
}

plugins {
    id 'com.gradle.build-scan'          version '1.13.4'
    id 'org.asciidoctor.convert'        version '1.5.8.1'
    id 'com.jfrog.bintray'              version '1.8.4'
    id 'org.ajoberstar.grgit'           version '2.2.0'
    id 'org.ajoberstar.git-publish'     version '0.4.1'
}

buildScan {
    if (System.getenv('CI')) {
        publishAlways()
        tag 'CI'
    }
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    //apply plugin: 'findbugs'

    version = consensusjVersion     // set in gradle.properties
    group = 'com.msgilligan'

    repositories {
        jcenter()
    }

//    tasks.withType(FindBugs) {
//        reports {
//            xml.enabled false
//            html.enabled true
//        }
//    }
}

subprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        implementation "org.slf4j:slf4j-api:${slf4jVersion}"

        testCompile "org.codehaus.groovy:groovy:${groovyVersion}:indy"
        testCompile("org.spockframework:spock-core:${spockVersion}") {
            exclude module: "groovy-all"
        }

        testRuntimeOnly "net.bytebuddy:byte-buddy:1.8.21"                 // allows Spock to mock classes (in addition to interfaces)
        testRuntimeOnly "org.objenesis:objenesis:2.6"                     // Allow Spock to mock classes with constructor arguments
        testRuntimeOnly  "org.slf4j:slf4j-jdk14:${slf4jVersion}"          // Runtime implementation of slf4j
    }

    // See http://blog.freeside.co/2014/06/24/gradle-and-groovys-invoke-dynamic-support/
    configurations.all {
        resolutionStrategy {
            force "org.codehaus.groovy:groovy:${groovyVersion}:indy"
            force "org.codehaus.groovy:groovy-json:${groovyVersion}:indy"
        }
    }

    compileJava {
        options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
    }

    tasks.withType(GroovyCompile) {
        groovyOptions.optimizationOptions.indy = true
    }
}

apply from: 'gradle/idea.gradle'
apply from: 'gradle/javadoc.gradle'
apply from: 'gradle/asciidoctor.gradle'
apply from: 'gradle/github-pages.gradle'
apply from: 'gradle/bintray.gradle'

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

build.dependsOn subprojects.build

task buildCI(dependsOn: [build, testReport, javadocAll, asciidoctor])
