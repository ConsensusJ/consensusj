buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctorj-diagram:1.5.4.1'
    }
}

plugins {
    id 'org.asciidoctor.convert'        version '1.5.3'
    id 'com.jfrog.bintray'              version '1.7.3'
    id 'org.ajoberstar.grgit'           version '1.7.2'
    id 'org.ajoberstar.github-pages'    version '1.7.2'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    //apply plugin: 'findbugs'

    version = bitcoinjAddonsVersion     // set in gradle.properties
    group = 'com.msgilligan'

    repositories {
        jcenter()
    }

//    tasks.withType(FindBugs) {
//        reports {
//            xml.enabled false
//            html.enabled true
//        }
//    }
}

subprojects {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        compile "org.bitcoinj:bitcoinj-core:${bitcoinjVersion}"
        compile "org.slf4j:slf4j-api:${slf4jVersion}"

        testCompile "org.codehaus.groovy:groovy:${groovyVersion}:indy"
        testCompile("org.spockframework:spock-core:${spockVersion}") {
            exclude module: "groovy-all"
        }

        testRuntime  "org.slf4j:slf4j-jdk14:${slf4jVersion}"          // Runtime implementation of slf4j
    }

    // See http://blog.freeside.co/2014/06/24/gradle-and-groovys-invoke-dynamic-support/
    configurations.all {
        resolutionStrategy {
            force "org.codehaus.groovy:groovy:${groovyVersion}:indy"
            force "org.codehaus.groovy:groovy-json:${groovyVersion}:indy"
        }
    }

    compileJava {
        options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
    }

    tasks.withType(JavaCompile) {
        doFirst {
            if (sourceCompatibility == '1.7' && System.env.JDK7_HOME != null) {
                options.bootClasspath = "$System.env.JDK7_HOME/jre/lib/rt.jar"
                //options.bootClasspath += "$File.pathSeparator$System.env.JDK7_HOME/jre/lib/jce.jar"
                // use the line above as an example to add jce.jar
                // and other specific JDK jars
            }
        }
    }

    tasks.withType(GroovyCompile) {
        groovyOptions.optimizationOptions.indy = true
    }
}

apply from: 'gradle/idea.gradle'
apply from: 'gradle/javadoc.gradle'
apply from: 'gradle/asciidoctor.gradle'
apply from: 'gradle/github-pages.gradle'
apply from: 'gradle/bintray.gradle'

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

build.dependsOn subprojects.build

task buildCI(dependsOn: [build, testReport, javadocAll, asciidoctor])
task jenkinsBuild(dependsOn: buildCI)
