plugins {
    id 'java-library'
}

def xChangeVersion = '5.2.2'

dependencies {
    api "javax.money:money-api:${javaMoneyApiVersion}"
    api "io.reactivex.rxjava3:rxjava:${rxJavaVersion}"
    api "org.knowm.xchange:xchange-core:${xChangeVersion}"

    implementation "org.javamoney.moneta:moneta-core:${javaMoneyMonetaVersion}"
    implementation "org.javamoney.moneta:moneta-convert:${javaMoneyMonetaVersion}"

    testImplementation project(':consensusj-currency')  // For BTC Currency Code
    testRuntimeOnly "org.knowm.xchange:xchange-bitfinex:${xChangeVersion}"
    testRuntimeOnly "org.knowm.xchange:xchange-itbit:${xChangeVersion}"
    testRuntimeOnly "org.knowm.xchange:xchange-coinbase:${xChangeVersion}"
    testRuntimeOnly "org.knowm.xchange:xchange-coinbasepro:${xChangeVersion}"
    testRuntimeOnly "org.knowm.xchange:xchange-bittrex:${xChangeVersion}"
}

ext.moduleName = 'org.consensusj.exchange'

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes  'Automatic-Module-Name': moduleName,
                'Implementation-Version': archiveVersion.get()
    }
}

test {
    testLogging.showStandardStreams = true
    systemProperty 'java.util.logging.config.file', "${project.projectDir}/src/test/logging.properties"
    beforeTest { descriptor ->
        logger.lifecycle('    ' + descriptor.getName())
    }
}


