plugins {
    id("groovy")
    id("io.micronaut.application") version "${micronautAppGradlePluginVersion}"
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

micronaut {
    version micronautVersion
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("org.consensusj.daemon.micronaut.*")
    }
}

graalvmNative {
    binaries {
        configureEach {
            buildArgs.add('-H:+UnlockExperimentalVMOptions')
        }
        main {
            imageName = 'walletd'
            //verbose = true
            configurationFileDirectories.from(file('src/main/graal'))
            buildArgs.add('-H:-CheckToolchain')
        }
        test {
            buildArgs.add("--initialize-at-build-time=org.junit.platform.commons.logging")
        }
    }
}

dependencies {
    implementation project(':cj-btc-services')
    implementation project(':consensusj-jsonrpc')

    implementation "io.micronaut:micronaut-runtime"
    implementation("io.micronaut:micronaut-websocket")
    implementation("io.micronaut.toml:micronaut-toml")
    implementation "io.micronaut:micronaut-jackson-databind"
    implementation "jakarta.annotation:jakarta.annotation-api"

    compileOnly "org.graalvm.nativeimage:svm"

    testImplementation project(':cj-btc-jsonrpc')
    // Tests are automatically configured by `micronaut.testRuntime("junit5")` above

    runtimeOnly "ch.qos.logback:logback-classic"
}

application {
    mainClass.set("org.consensusj.daemon.micronaut.Application")
}

dockerBuild {
    images = ["${System.env.DOCKER_IMAGE ?: project.name}:$project.version"]
}

dockerBuildNative {
    images = ["${System.env.DOCKER_IMAGE ?: project.name}:$project.version"]
}
