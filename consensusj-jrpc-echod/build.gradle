plugins {
    id("groovy")
    id("io.micronaut.application") version "${micronautAppGradlePluginVersion}"
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

micronaut {
    version micronautVersion
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("org.consensusj.jsonrpc.daemon.*")
    }
}

graalvmNative {
    binaries {
        main {
            imageName = 'jrpc-echod'
            buildArgs.add('-H:+UnlockExperimentalVMOptions')
            buildArgs.add('-H:-CheckToolchain')
            // SharedArenaSupport seems to require GraalVM 25
            // buildArgs.add('-H:+SharedArenaSupport')
        }
        test {
            buildArgs.add('-H:+UnlockExperimentalVMOptions')
            buildArgs.add("--initialize-at-build-time=org.junit.platform.commons.logging")
        }
    }
}

dependencies {
    implementation project(':consensusj-jsonrpc')

    implementation "io.micronaut:micronaut-runtime"
    implementation "io.netty:netty-transport-native-unix-common"
    implementation "io.micronaut:micronaut-jackson-databind"
    implementation "jakarta.annotation:jakarta.annotation-api"

    compileOnly "org.graalvm.nativeimage:svm"

    runtimeOnly "org.yaml:snakeyaml"
    runtimeOnly "ch.qos.logback:logback-classic"

    // Tests are automatically configured by `micronaut.testRuntime("junit5")` above
}

application {
    mainClass.set("org.consensusj.jsonrpc.daemon.Application")
}

dockerBuild {
    images = ["${System.env.DOCKER_IMAGE ?: project.name}:$project.version"]
}

dockerBuildNative {
    images = ["${System.env.DOCKER_IMAGE ?: project.name}:$project.version"]
}
