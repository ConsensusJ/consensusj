plugins {
    id("groovy")
    id("io.micronaut.application") version "${micronautAppGradlePluginVersion}"
}

tasks.withType(JavaCompile) {
    options.release = 17
}

micronaut {
    runtime("netty")
    testRuntime("spock2")
    processing {
        incremental(true)
        annotations("org.consensusj.daemon.micronaut.*")
    }
}

graalvmNative {
    binaries {
        main {
            imageName = 'jbitcoind'
            //verbose = true
            configurationFileDirectories.from(file('src/main/graal'))
        }
    }
}

dependencies {
    implementation("io.micronaut:micronaut-runtime")
    implementation("javax.annotation:javax.annotation-api")

    implementation project(':consensusj-jsonrpc')
    implementation project(':cj-btc-json')
    implementation project(':cj-btc-services')

    compileOnly("org.graalvm.nativeimage:svm")

    implementation('org.slf4j:slf4j-api:1.7.36') {
        force = true
    }

    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"
}

application {
    mainClass.set("org.consensusj.daemon.micronaut.Application")
}

dockerBuild {
    images = ["${System.env.DOCKER_IMAGE ?: project.name}:$project.version"]
}

dockerBuildNative {
    images = ["${System.env.DOCKER_IMAGE ?: project.name}:$project.version"]
}
