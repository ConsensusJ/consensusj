plugins {
    id("groovy")
    id("com.github.johnrengelman.shadow") version "${micronautShadowVersion}"
    id("io.micronaut.application") version "${micronautAppGradlePluginVersion}"
}

micronaut {
    runtime("netty")
    testRuntime("spock2")
    processing {
        incremental(true)
        annotations("org.consensusj.daemon.micronaut.*")
    }
}

graalvmNative {
    binaries {
        main {
            imageName = 'jbitcoind'
        }
    }
}

dependencies {
    implementation("io.micronaut:micronaut-runtime")
    implementation("javax.annotation:javax.annotation-api")

    implementation project(':consensusj-jsonrpc')
    implementation project(':cj-btc-json')
    implementation project(':cj-btc-services')

    compileOnly("org.graalvm.nativeimage:svm")

    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"
}

application {
    mainClass.set("org.consensusj.daemon.micronaut.Application")
}

java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

dockerBuild {
    images = ["${System.env.DOCKER_IMAGE ?: project.name}:$project.version"]
}

dockerBuildNative {
    images = ["${System.env.DOCKER_IMAGE ?: project.name}:$project.version"]
}
